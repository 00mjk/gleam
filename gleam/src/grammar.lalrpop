// vi: ft=rust

use std::str::FromStr;

use crate::ast::{Expr, Clause, Scope, Arg, Module, Statement, Type};
use crate::pattern::Pattern;
use crate::parser::*;

grammar;

pub Module: Module<()> = {
    Statement* => Module {
        name: "".to_string(),
        statements: <>,
    }
}

Statement: Statement<()> = {
    StatementFun => <>,
    StatementTest => <>,
    // StatementEnum => <>,
    StatementExternalFun => <>,
    StatementExternalType => <>,
    StatementImport => <>,
}

StatementExternalFun: Statement<()> = {
    <s:@L> <p:"pub"?> "external" "fn" <n:VarName> "(" <a:Comma<Type>> ")" "->" <r:Type> "=" <m:RawAtom> <f:RawAtom> <e:@L> => Statement::ExternalFun {
        meta: meta(s, e),
        public: p.is_some(),
        name: n,
        args: a,
        module: m,
        fun: f,
        retrn: r,
    }
}

StatementFun: Statement<()> = {
    <s:@L> <p:"pub"?> "fn" <n:VarName> "(" <a:Comma<FnArg>> ")" "{" <b:Expr> "}" <e:@L> => Statement::Fun {
        meta: meta(s, e),
        public: p.is_some(),
        name: n,
        args: a,
        body: b,
    }
}

StatementTest: Statement<()> = {
    <s:@L> "test" <n:VarName> "{" <b:Expr> "}" <e:@L> => Statement::Test {
        meta: meta(s, e),
        name: n,
        body: b,
    }
}

StatementImport: Statement<()> = {
    <s:@L> "import" <n:VarName> <e:@L> => Statement::Import {
        meta: meta(s, e),
        module: n,
    }
}

StatementExternalType: Statement<()> = {
    <s:@L> <p:"pub"?> "external" "type" <n:TypeName> <e:@L> => Statement::ExternalType {
        meta: meta(s, e),
        public: p.is_some(),
        name: n.0,
        args: n.1,
    }
}

pub Expr: Expr<()> = {
    Let => <>,
    Seq => <>,
    SingleExpr => <>,
}

SingleExpr: Expr<()> = {
    Nil => <>,
    Int => <>,
    Var => <>,
    Fun => <>,
    Cons => <>,
    Call => <>,
    Atom => <>,
    Float => <>,
    Tuple => <>,
    String => <>,
    // BinOp => <>,
    // Case => <>,
    RecordNil => <>,
    Constructor => <>,
    // RecordCons => <>,
    RecordSelect => <>,
    ModuleSelect => <>,
}

Let: Expr<()> = {
    <s:@L> <p:Pattern> "=" <v:SingleExpr> <e:@L> <t:Expr> => Expr::Let {
        meta: meta(s, e),
        typ: (),
        value: Box::new(v),
        clause: Clause {
            meta: meta(s, e),
            typ: (),
            pattern: p,
            then: Box::new(t),
        }
    }
}

RecordSelect: Expr<()> = {
    <s:@L> <r:SingleExpr> "." <l:VarName> <e:@L> => Expr::RecordSelect {
        meta: meta(s, e),
        typ: (),
        label: l,
        record: Box::new(r),
    },
}

ModuleSelect: Expr<()> = {
    <s:@L> <m:SingleExpr> ":" <l:VarName> <e:@L> => Expr::ModuleSelect {
        meta: meta(s, e),
        typ: (),
        label: l,
        module: Box::new(m),
    },
}

Seq: Expr<()> = {
    <s:@L> <a:SingleExpr> <b:Expr> <e:@L> => Expr::Seq {
        meta: meta(s, e),
        first: Box::new(a),
        then: Box::new(b),
    }
}

Fun: Expr<()> = {
    <s:@L> "fn(" <a:Comma<FnArg>> ")" "{" <b:Expr> "}" <e:@L> => Expr::Fun {
        meta: meta(s, e),
        typ: (),
        args: a,
        body: Box::new(b),
    }
}

FnArg: Arg = {
    VarName => Arg {
        name: <>,
    },
}

Constructor: Expr<()> = {
    <s:@L> <n:ConstructorName> <e:@L> => Expr::Constructor {
        meta: meta(s, e),
        typ: (),
        name: n,
    },
}

Call: Expr<()> = {
    <s:@L> <fun:SingleExpr> "(" <args:Comma<Expr>> ")" <e:@L> => Expr::Call {
        meta: meta(s, e),
        typ: (),
        fun: Box::new(fun),
        args: args,
    }
}

Tuple: Expr<()> = {
    <s:@L> "{" <elems:Comma<Expr>> "}" <e:@L> => Expr::Tuple {
        meta: meta(s, e),
        typ: (),
        elems: elems,
    }
}

Cons: Expr<()> = {
    <s:@L> "[" <head:Expr> "|" <tail:Expr> "]" <e:@L> => Expr::Cons {
        meta: meta(s, e),
        typ: (),
        head: Box::new(head),
        tail: Box::new(tail),
    }
}

Var: Expr<()> = {
    <s:@L> <v:VarName> <e:@L> => Expr::Var {
        meta: meta(s, e),
        typ: (),
        name: v,
        scope: Scope::Local,
    }
}

Nil: Expr<()> = {
    <s:@L> "[]" <e:@L> => Expr::Nil {
        meta: meta(s, e),
        typ: (),
    }
}

RecordNil: Expr<()> = {
    <s:@L> "{}" <e:@L> => Expr::RecordNil {
        meta: meta(s, e),
    }
}

// TODO: Unescape?
Atom: Expr<()> = {
    <s:@L> <x:RawAtom> <e:@L> => Expr::Atom {
        meta: meta(s, e),
        value: x,
    }
}

// TODO: Unescape?
String: Expr<()> = {
    <s:@L> <x:r#""(([^\\"]|\\.)*)""#> <e:@L> => Expr::String {
        meta: meta(s, e),
        value: x[1..(x.len() - 1)].to_string(),
    }
}

Int: Expr<()> = {
    <s:@L> <i:r"-?[0-9]+"> <e:@L> => Expr::Int {
        meta: meta(s, e),
        value: i64::from_str(i).unwrap(),
    }
}

Float: Expr<()> = {
    <s:@L> <f:r"-?[0-9]+\.+[0-9]*"> <e:@L> => Expr::Float {
        meta: meta(s, e),
        value: f64::from_str(f).unwrap(),
    }
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Pattern: Pattern = {
    PatternVar => <>,
    // PatternInt => <>,
    // PatternNil => <>,
    // PatternCons => <>,
    // PatternAtom => <>,
    // PatternEnum => <>,
    // PatternFloat => <>,
    // PatternTuple => <>,
    // PatternString => <>,
    // PatternRecordNil => <>,
    // PatternRecordCons => <>,
}

PatternVar: Pattern = {
    <s:@L> <v:VarName> <e:@L> => Pattern::Var {
        meta: meta(s, e),
        name: v,
    }
}

Type: Type = {
    TypeConstructor => <>,
    TypeVar => <>,
}

TypeConstructor: Type = {
    <s:@L> <n:r"[A-Z][A-Za-z]*"> <e:@L> => Type::Constructor {
        meta: meta(s, e),
        args: vec![],
        name: n.to_string(),
    },
    <s:@L> <n:r"[A-Z][A-Za-z]*"> "(" <a:Comma<Type>> ")" <e:@L> => Type::Constructor {
        meta: meta(s, e),
        args: a,
        name: n.to_string(),
    },
}

TypeVar: Type = {
    <s:@L> <n:VarName> <e:@L> => Type::Var {
        meta: meta(s, e),
        name: n,
    }
}

TypeName: (String, Vec<String>) = {
    <v:r"[A-Z][A-Za-z]*"> => (v.to_string(), vec![]),
    <v:r"[A-Z][A-Za-z]*"> "(" <a:Comma<VarName>> ")" => (v.to_string(), a),
}

VarName: String = {
    <v:r"[a-z][A-za-z_]*"> => v.to_string()
}

ConstructorName: String = {
    <v:r"[A-Z][A-Za-z]*"> => v.to_string()
}

RawAtom: String = {
    <x:r"'(([^\\']|\\.)*)'"> => x[1..(x.len() - 1)].to_string(),
}
